#    bas
#    an airplug compatible program
#    author: Bertrand Ducourthial AT utc.fr, Anthony Buisset (v0.1)
#    license type: free of charge license for academic and research purpose
#    see license.txt
### MODULE EMISSION ###########################################################

### VARIABLES DU MODULE #######################################################

# Message par defaut pour l'emission
set BAS_snd_msg $BAS_rc_msgdefault

# Application par defaut vers qui sera emis le message
set BAS_snd_app $BAS_rc_appdefault

# Delai par defaut entre deux messages.
# NB : peut etre modifie par option --delay=
set BAS_snd_delay $BAS_rc_delaydefault

# Mnemonique pour retrouver le payload dans les messages
set BAS_snd_mnemopayload $BAS_rc_mnemopayload

# Mnemonique pour retrouver le numero de sequence dans les messages
set BAS_snd_mnemonseq $BAS_rc_mnemonseq

# Demande de depart automatique des emissions periodiques
# NB : peut etre change par l'option --autosend
set BAS_snd_autosend false

# Etat d'emission periodique en cours ou non.
set BAS_snd_sending false

# Numero du message emis
set BAS_snd_nseq 0
###############################################################################

### ZONE DU MODULE ############################################################
labelframe .snd -pady 2 -padx 2 -text "Emission \[$APP, ident = $APG_ident\]" -fg $APG_int_coltitle

button .snd.btauto -text "Auto" \
		-activebackground $APG_int_colbutton \
		-foreground $APG_int_colbutton \
		-font $APG_int_fnbutton \
		-width 10 \
		-state disable \
		-command BAS_snd_btauto


labelframe .snd.app -text "app"
entry .snd.app.v -width 4 -textvariable BAS_snd_app

labelframe .snd.del -text "d√©lai (ms)"
set value $BAS_snd_delay
spinbox .snd.del.v -width 6 -textvariable BAS_snd_delay -from 100 -to 10000 -increment 100 -justify right
.snd.del.v set $value

labelframe .snd.msg -text "message"
entry .snd.msg.v -width 64 -textvariable BAS_snd_msg

labelframe .snd.nseq -text "nseq"
label .snd.nseq.v -width 6 -textvariable BAS_snd_nseq -anchor e

# affichage des sous-zones de la zone snd
pack .snd.btauto .snd.del .snd.app .snd.msg .snd.app .snd.nseq -side left -fill y -pady 2
pack .snd.msg.v .snd.app.v .snd.del.v .snd.nseq.v
###############################################################################


### OPTIONS DE LA LIGNE DE COMMANDE ###########################################
foreach option $APG_lstargs {
    set name [lindex [split $option "="] 0]
    set value [lindex [split $option "="] 1]
    
    switch -- $name {
				--dest {
						# application destinataire ----------------------------------------#
						if { $value == "" } {
								APG_vrb_dispwarning "option $name trouvee sans argument, utilisation de la valeur par defaut : $BAS_snd_app"
						} else {
								set BAS_snd_app $value
								APG_vrb_dispnotif "option $option trouvee, nouvelle application : $BAS_snd_app"
						}
				}
				
				--delay {
						# periode d'emission ----------------------------------------------#
						if { $value == "" } {
								APG_vrb_dispwarning "option $name trouvee sans argument, utilisation de la valeur par defaut : $BAS_snd_delay"
						} else {
								set BAS_snd_delay $value
								APG_vrb_dispnotif "option $option trouvee, nouvelle periode : $BAS_snd_delay"
						}
				}

				--autosend {
						# emission automatique --------------------------------------------#
						set BAS_snd_autosend true
						APG_vrb_dispnotif "option $option trouvee, mode emission automatique active"
				}
    }
}
###############################################################################

### PROCEDURE DE L'INTERFACE ##################################################
proc BAS_snd_btauto { } {

		if { $::BAS_snd_sending == false } {
				.snd.btauto configure -text "Stop"
				set ::BAS_snd_sending true
				BAS_snd_autosend
		} else {
				after cancel BAS_snd_autosend
				set ::BAS_snd_sending false
				.snd.btauto configure -text "Auto"
		}
}
###############################################################################

### PROCEDURES DU MODULE ######################################################

#-- Procedure BAS_snd_send ---------------------------------------------------#
# Action : envoie periodiquement le message sur le reseau                     #
# Entree : rien                                                               #
# Retour : rien                                                               #
#-----------------------------------------------------------------------------#
proc BAS_snd_autosend { } {
    
		# Incrementation du numero de message
		incr ::BAS_snd_nseq

    # Creation du message formate
    set msg [APG_msg_createmsg $::BAS_snd_mnemopayload $::BAS_snd_msg]

		# Ajout du numero de sequence
		APG_msg_addmsg msg $::BAS_snd_mnemonseq $::BAS_snd_nseq

    APG_send_whatwho $msg $::BAS_snd_app
		
		after $::BAS_snd_delay BAS_snd_autosend
}

